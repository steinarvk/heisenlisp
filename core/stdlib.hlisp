(defun! list (&rest xs) xs)

(defun! + (&rest xs) (reduce-left low-level-plus 0 xs))
(defun! * (&rest xs) (reduce-left low-level-multiply 1 xs))
(defun! / (numerator &rest denominators)
  (if (nil? denominators)
      (/ 1 numerator)
      (low-level-divide numerator (apply * denominators))))

(defun! nan? (x)
  (let ((xs (possible-values x)))
    (and (_cons? xs) (any? _nan?  xs))))

(defun! inc (n) (+ n 1))
(defun! dec (n) (- n 1))

(defmacro! progn (&rest body) `(let () ,@body))

(defun! negate (x) (* -1 x))

(defun! - (&rest xs)
  (if (_atom-eq? 1 (length xs))
      (negate (first xs))
      (reduce-left low-level-minus 0 xs)))

(defun! empty? (xs) (nil? xs))

(defun! zero? (n) (_atom-eq? 0 n))
(set! remainder mod)

(defun! first (xs) (car xs))
(defun! second (xs) (car (cdr xs)))
(defun! third (xs) (car (cdr (cdr xs))))

(defun! _dumb-equals? (a b)
  (_atom-eq? (_to-string a) (_to-string b)))

(defmacro! cond (clause &rest more-clauses)
  `(if ,(first clause)
       ,(second clause)
       ,(if (nil? more-clauses)
            nil
            (cons 'cond more-clauses))))

(defun! _cons? (x) (equals? 'cons (_type x)))

(defun! uncertain? (x)
  (if (_cons? x)
      (or (uncertain? (car x))
          (uncertain? (cdr x)))
      (_uncertain? x)))

(defmacro! when (condition &rest body)
  `(if ,condition (progn ,@body) nil))

(defmacro! unless (condition &rest body)
  `(if (not ,condition) (progn ,@body) nil))

(defun! _maybe? (x)
  (and (= 'any-of (_type x))
       (let ((vs (possible-values x)))
         (or (= (list true false) vs)
             (= (list false true) vs)))))

(defun! even? (n) (and (integer? n) (= 0 (mod n 2))))
(defun! odd? (n) (and (integer? n) (not (even? n))))

(defun! contains? (x xs) (any? (lambda (y) (= x y)) xs))

(defun! is-equal-to (x) (lambda (y) (= x y)))
(defun! is-not-equal-to (x) (lambda (y) (not (= x y))))
